apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion android_compile_sdk_version
    buildToolsVersion android_build_tools_version

    defaultConfig {
        minSdkVersion android_min_sdk_version
        targetSdkVersion android_target_sdk_version
        consumerProguardFiles 'autobundle-proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    api project(':autobundle-annotations')
    compileOnly project(':autobundle-dispatcher')
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.source
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = repository_name
                url = repository_url
                credentials {
                    username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                    password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact tasks.androidSourcesJar
                groupId = metadata.groupId
                artifactId = 'autobundle'
                version = metadata.version
                pom {
                    description = metadata.desc
                    url = metadata.website
                }
            }
        }
    }
}

