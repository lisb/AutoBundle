import org.gradle.internal.jvm.Jvm

apply plugin: 'java'
apply plugin: 'maven-publish'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

dependencies {
    implementation project(':autobundle-annotations')
    implementation 'com.google.auto.service:auto-service:1.0-rc2'
    annotationProcessor 'com.google.auto.service:auto-service:1.0-rc2'
    implementation 'com.squareup:javapoet:1.7.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.27'
    testImplementation 'com.google.android:android:4.1.1.4'
    testImplementation 'com.google.android:support-v4:r7'
    testImplementation 'com.google.testing.compile:compile-testing:0.8'
    testImplementation files(Jvm.current().getToolsJar())
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = repository_name
            url = repository_url
            credentials {
                username = localProps["gpr.user"] ?: System.getenv("GPR_USER")
                password = localProps["gpr.key"] ?: System.getenv("GPR_KEY")
            }
        }
    }
    publications {
        release(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            groupId = metadata.groupId
            artifactId = 'autobundle-processor'
            version = metadata.version
            pom {
                description = metadata.desc
                url = metadata.website
            }
        }
    }
}